\startMPpage

dx := 4cm;
dy := 4cm;

z0 = origin;
%ligne 1 noeuds 1 à 6
for i = 1 upto 5:
    y[i] = y[i+1];
    x[i+1] - x[i] = dx;
endfor;

%ligne 2 noeuds 8 à 13
for i = 8 upto 12:
    y[i] = y[i+1];
    x[i+1] - x[i] = dx;
endfor;

%ligne 3 noeuds 16 à 21
for i = 16 upto 20:
    y[i] = y[i+1];
    x[i+1] - x[i] = dx;
endfor;

2x0 = x1 + x6;
x0 = x7 = x14 = x15 = x22;

x1 = x8 = x16;

y0 - y1 = y1 - y7 = y7 - y8 = y8 - y14 = y14 - y15 = y15 - y16 = y16 - y22 = dy;

path n[];
for i = 0 upto 22:
    n[i] := fullcircle scaled 1.6cm shifted z[i];
    draw n[i] withpen pencircle scaled 1.5pt;
endfor;

vardef connect(expr i, j, p) =
    p cutbefore n[i] cutafter n[j]
enddef;

drawoptions(withpen pencircle scaled 2pt withcolor blue);

drawarrow connect(0, 1, z0{left}..{down}z1);
drawarrow connect(0, 6, z0{right}..{down}z6);
drawarrow connect(1, 7, z1{down} .. tension 2 .. {down+right} z7);
drawarrow connect(6, 7, z6{down} .. tension 2 .. {down+left} z7);

drawarrow connect(7, 8, z7{left+down} .. tension 2 .. {down} z8);
drawarrow connect(7, 13, z7{right+down} .. tension 2 .. {down} z13);
drawarrow connect(8, 14, z8{down} .. tension 2 .. {down+right} z14);
drawarrow connect(13, 14, z13{down} .. tension 2 .. {down+left} z14);

drawarrow connect(15, 16, z15{left+down} .. tension 2 .. {down} z16);
drawarrow connect(15, 21, z15{right+down} .. tension 2 .. {down} z21);
drawarrow connect(16, 22, z16{down} .. tension 2 .. {down+right} z22);
drawarrow connect(21, 22, z21{down} .. tension 2 .. {down+left} z22);

for i = 1 upto 5:
    drawoptions(withpen pencircle scaled 2pt withcolor darkgreen);
    drawarrow connect(i, i+1, z[i]{up+right}..{down+right}z[i+1]);
    drawoptions(withpen pencircle scaled 2pt withcolor red);
    drawarrow connect(i+1, i, z[i+1]{down+left}..{up+left}z[i]);
endfor;

for i = 8 upto 12:
    drawoptions(withpen pencircle scaled 2pt withcolor darkgreen);
    drawarrow connect(i, i+1, z[i]{up+right}..{down+right}z[i+1]);
    drawoptions(withpen pencircle scaled 2pt withcolor red);
    drawarrow connect(i+1, i, z[i+1]{down+left}..{up+left}z[i]);
endfor;
for i = 16 upto 20:
    drawoptions(withpen pencircle scaled 2pt withcolor darkgreen);
    drawarrow connect(i, i+1, z[i]{up+right}..{down+right}z[i+1]);
    drawoptions(withpen pencircle scaled 2pt withcolor red);
    drawarrow connect(i+1, i, z[i+1]{down+left}..{up+left}z[i]);
endfor;

drawoptions();
label("$s$",  z0);
label("$\dots$", 0.5[z3,z4]);
label("$\dots$", 0.5[z10,z11]);
label("$\dots$", 0.5[z18,z19]);
label("$\vdots$", 0.5[z14,z15]);

label("$v_{1, 1}$", z1);
label("$v_{1, 2}$", z2);
label("$v_{1, 3}$", z3);
label("$v_{1, 2m-2}$", z4);
label("$v_{1, 2m-1}$", z5);
label("$v_{1, 2m}$", z6);
label("$d_1$", z7);
label("$v_{2, 1}$", z8);
label("$v_{2, 2}$", z9);
label("$v_{2, 3}$", z10);
label("$v_{2, 2m-2}$", z11);
label("$v_{2, 2m-1}$", z12);
label("$v_{2, 2m}$", z13);
label("$d_2$", z14);
label("$d_{n-1}$", z15);

label("$v_{n, 1}$", z16);
label("$v_{n, 2}$", z17);
label("$v_{n, 3}$", z18);
label("$v_{n, 2m-2}$", z19);
label("$v_{n, 2m-1}$", z20);
label("$v_{n, 2m}$", z21);
label("$t$",  z22);
\stopMPpage
